# Maintainer: Aaron Fischer <mail@aaron-fischer.net>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: trile7 at gmail dot com
# Contributor: Ernia <monghitri@aruba.it>
# manuel (see forum.antergos.com) 2019-01-25:
# - changed $url, now directly from the developer
# - added sha512sums
# - added _version_check

pkgname=yad
pkgver=0.42.0
pkgrel=0
pkgdesc="A fork of zenity - display graphical dialogs from shell scripts or command line"
url=https://github.com/v1cont/yad
arch=('x86_64')
license=('GPL3')
depends=('gtk3' 'webkit2gtk')
makedepends=('autoconf' 'automake' 'intltool')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=(700b11f449b63ec1bc8fecf0d2df3ed18bc7078472de0f3ee034c28e3e096eb61559529cc094c76fc8a9959bbb3e5f159d621ad9739d857b0d9b8d7ff386deaa)

_version_check()
{
  local tmp=$(mktemp)

  wget -q -O "$tmp" $url/releases/latest
  if [ $? -eq 0 ] ; then
    local newver="$(grep "Release YAD " $tmp | sed -e 's|^.*Release YAD ||' -e 's| .*$||')"
    if [ "$newver" != "$pkgver" ] ; then
      echo "PKGBUILD is outdated ($pkgver -> $newver), please fix it." >&2
      echo "The sha512sums must be updated, too." >&2
      unset pkgver
    fi
  else
    echo "Error: could not check version info." >&2
    unset pkgver
  fi
  rm -f "$tmp"
}

_version_check
unset -f _version_check


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    autoreconf -ivf
    intltoolize
    ./configure --prefix=/usr --with-gtk=gtk3 --enable-icon-browser --enable-html
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
